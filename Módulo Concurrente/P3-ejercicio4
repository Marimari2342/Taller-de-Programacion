programa MoverFlores
procesos
  proceso moverUnaFlor(E inAv:numero;E inCa:numero;E id:numero;ES hay:boolean)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    si HayFlorEnLaEsquina
      tomarFlor
      BloquearEsquina(11,11)
      Pos(11,11)
      LiberarEsquina(10,10)
      depositarFlor
      Pos(inAv,inCa)
      LiberarEsquina(11,11)
    sino
      Pos(inAv,inCa)
      LiberarEsquina(10,10)
      EnviarMensaje(id,jefe)
      hay:= F
  fin
areas
  areaC1:AreaPC(10,10,10,10)
  areaC2:AreaC(11,11,11,11)
  areaR1:AreaP(9,9,9,9)
  areaR2:AreaP(9,10,9,10)
  areaR3:AreaP(9,11,9,11)
  areaR4:AreaP(9,12,9,12)
  areaJefe:AreaP(1,1,1,1)
robots 
  robot recolector
  variables
    id:numero
    hay:boolean
  comenzar
    hay:= V
    RecibirMensaje(id,jefe)
    mientras hay
      moverUnaFlor(PosAv,PosCa,id,hay)
  fin
  robot jefeRobot
  variables
    ultimo:numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    EnviarMensaje(4,r4)
    repetir 4
      RecibirMensaje(ultimo,*)
    Informar('ultimoRobot ',ultimo)
    Pos(11,11)
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
variables
  r1,r2,r3,r4:recolector
  jefe:jefeRobot
comenzar
  AsignarArea(r1,areaC1)
  AsignarArea(r1,areaC2)
  AsignarArea(r1,areaR1)
  AsignarArea(r2,areaC1)
  AsignarArea(r2,areaC2)
  AsignarArea(r2,areaR2)
  AsignarArea(r3,areaC1)
  AsignarArea(r3,areaC2)
  AsignarArea(r3,areaR3)
  AsignarArea(r4,areaC1)
  AsignarArea(r4,areaC2)
  AsignarArea(r4,areaR4)
  AsignarArea(jefe,areaC2)
  AsignarArea(jefe,areaJefe)
  Iniciar(jefe,1,1)
  Iniciar(r1,9,9)
  Iniciar(r2,9,10)
  Iniciar(r3,9,11)
  Iniciar(r4,9,12)
fin